apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply from: "$rootDir/gradle/gradle-quality.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    publishNonDefault true

    defaultConfig {
        applicationId "com.evernote.android.state.demo"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles 'proguard.cfg'
        }

        proguard.initWith(buildTypes.debug)
        proguard {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        test {
            java.srcDirs += 'src/test/kotlin'
        }
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            testLogging {
                events 'failed', 'standardOut', 'standardError'
            }
        }
    }

    lintOptions {
        abortOnError true
    }
}

dependencies {
    compile project(':library')
    kapt project(':processor')
    compile "com.android.support:appcompat-v7:$supportLibVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

uploadArchives.enabled false
